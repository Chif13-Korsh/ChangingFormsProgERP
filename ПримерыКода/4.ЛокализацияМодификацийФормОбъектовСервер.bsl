#Область ПрограммныйИнтерфейс

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
Процедура ПриСозданииНаСервереИзменениеФорм(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Если СтрНайти(Форма.ИмяФормы, "ФормаЭлемента") > 0 Тогда 
		ПриСозданииНаСервереФормаЭлемента(Форма, Отказ, СтандартнаяОбработка);	
		
	ИначеЕсли СтрНайти(Форма.ИмяФормы, "ФормаДокумента") > 0 Тогда 
		ПриСозданииНаСервереФормаДокумента(Форма, Отказ, СтандартнаяОбработка);	
		
	ИначеЕсли СтрНайти(Форма.ИмяФормы, "ФормаСписка") > 0 Тогда 
		ПриСозданииНаСервереФормаСписка(Форма, Отказ, СтандартнаяОбработка);	
		
	ИначеЕсли СтрНайти(Форма.ИмяФормы, "ФормаВыбора") > 0 Тогда 
		ПриСозданииНаСервереФормаВыбора(Форма, Отказ, СтандартнаяОбработка);	
		
	Иначе
		ПриСозданииНаСервереПроизвольныеФормы(Форма, Отказ, СтандартнаяОбработка);	
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ДобавлениеПоляРеквизитаНаФормуОбъекта(Форма, ИмяПоля, ПутьКДанным, Заголовок, Родитель,   
				Ширина = 0, Гиперссылка = Ложь, ТолькоПросмотр = Ложь, РастягиватьПоГоризонтали = Неопределено,
				ИмяСобытия = "", ДействиеСобытия = "") Экспорт
	
	Попытка
		НовыйЭлемент = Форма.Элементы.Добавить(ИмяПоля, Тип("ПолеФормы"), Родитель);
		Если Гиперссылка = Истина Тогда
			НовыйЭлемент.Вид 		 = ВидПоляФормы.ПолеНадписи;
			НовыйЭлемент.Гиперссылка = Истина;
		Иначе
			НовыйЭлемент.Вид 	 	 = ВидПоляФормы.ПолеВвода;
		КонецЕсли;
		НовыйЭлемент.ПутьКДанным = ПутьКДанным;
		НовыйЭлемент.Заголовок 	 = Заголовок;

		НовыйЭлемент.ТолькоПросмотр = ТолькоПросмотр;
		НовыйЭлемент.КнопкаОчистки 	= НЕ ТолькоПросмотр;
		
		Если Ширина <> 0 Тогда
			НовыйЭлемент.АвтоМаксимальнаяШирина = Ложь;
			НовыйЭлемент.РастягиватьПоГоризонтали = Ложь;			
			НовыйЭлемент.Ширина = Ширина;
		Иначе
			НовыйЭлемент.АвтоМаксимальнаяШирина = Истина; 
			Если РастягиватьПоГоризонтали <> Неопределено Тогда
				НовыйЭлемент.РастягиватьПоГоризонтали = РастягиватьПоГоризонтали;	
			КонецЕсли;
		КонецЕсли;
		
		Если ИмяСобытия <> "" И ДействиеСобытия <> "" Тогда
			НовыйЭлемент.УстановитьДействие(ИмяСобытия, ДействиеСобытия);
		КонецЕсли;
		
		Возврат НовыйЭлемент;
		
	Исключение
		ЗаписьЖурналаРегистрации("Ошибка выполнения", УровеньЖурналаРегистрации.Ошибка, , 
			НСтр("ru = 'Программное добавление реквизита на форму'"),
			"Ошибка программного добавления реквизита " + ПутьКДанным + " с именем " + ИмяПоля + " на форму " + Форма.ИмяФормы + ":" + Символы.ПС + ОписаниеОшибки());
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

Функция ДобавлениеПоляФлагаНаФормуОбъекта(Форма, ИмяПоля, ПутьКДанным, Заголовок, Родитель = Неопределено, 
				ТолькоПросмотр = Ложь, Положение = Неопределено, ДействиеСобытия = "") Экспорт
				
	Если Положение = Неопределено Тогда
		Положение = ПоложениеЗаголовкаЭлементаФормы.Право;	
	КонецЕсли;
	
	Попытка
		НовыйЭлемент = Форма.Элементы.Добавить(ИмяПоля, Тип("ПолеФормы"), Родитель);
		НовыйЭлемент.Вид 		 = ВидПоляФормы.ПолеФлажка;
		НовыйЭлемент.ПутьКДанным = ПутьКДанным;
		НовыйЭлемент.Заголовок 	 = Заголовок;
		
		НовыйЭлемент.ПоложениеЗаголовка = Положение;
		НовыйЭлемент.ТолькоПросмотр 	= ТолькоПросмотр;
				
		Если Не ДействиеСобытия = "" Тогда
			НовыйЭлемент.УстановитьДействие("ПриИзменении", ДействиеСобытия);
		КонецЕсли;
		
		Возврат НовыйЭлемент;
		
	Исключение
		ЗаписьЖурналаРегистрации("Ошибка выполнения", УровеньЖурналаРегистрации.Ошибка,, НСтр("ru = 'Программное добавление реквизита на форму'"),
			"Ошибка программного добавления реквизита " + ПутьКДанным + " с именем " + ИмяПоля + " на форму " + Форма.ИмяФормы + ":" + Символы.ПС + ОписаниеОшибки());
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

#КонецОбласти 

#Область ПриСозданииНаСервере_ФормаДокумента

Процедура ПриСозданииНаСервереФормаДокумента(Форма, Отказ, СтандартнаяОбработка) Экспорт 	
	
	Если Форма.ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда 
		ПриСозданииНаСервере_РеализацияТоваровУслуг_ФормаДокумента(Форма, Отказ, СтандартнаяОбработка);
				
	ИначеЕсли Форма.ИмяФормы = "Документ.ВнутреннееПотребление.Форма.ФормаДокумента" Тогда 
		ПриСозданииНаСервере_ВнутреннееПотреблениеТоваров_ФормаДокумента(Форма, Отказ, СтандартнаяОбработка);

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПриСозданииНаСервере_ФормаЭлемента

Процедура ПриСозданииНаСервереФормаЭлемента(Форма, Отказ, СтандартнаяОбработка) Экспорт

	Если Форма.ИмяФормы = "Справочник.Номенклатура.Форма.ФормаЭлемента" Тогда 
		ПриСозданииНаСервере_Номенклатура_ФормаЭлемента(Форма, Отказ, СтандартнаяОбработка); 
		
	ИначеЕсли Форма.ИмяФормы = "Справочник.Контрагенты.Форма.ФормаЭлемента" Тогда  
		ПриСозданииНаСервере_Контрагенты_ФормаЭлемента(Форма, Отказ, СтандартнаяОбработка);

	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ПриСозданииНаСервере_ФормаВыбора

Процедура ПриСозданииНаСервереФормаВыбора(Форма, Отказ, СтандартнаяОбработка) Экспорт 
	
	МодификацияФормВыбораОбщая(Форма, Отказ, СтандартнаяОбработка);
	
	Если Форма.ИмяФормы = "Справочник.Подразделения.Форма.ФормаВыбораУправляемая" Тогда
		ПриСозданииНаСервере_Подразделения_ФормаВыбора(Форма, Отказ, СтандартнаяОбработка);	
	
	ИначеЕсли Форма.ИмяФормы = "Справочник.ДоговорыКонтрагентов.Форма.ФормаВыбора" Тогда
		ПриСозданииНаСервере_ДоговорыКонтрагентов_ФормаВыбора(Форма, Отказ, СтандартнаяОбработка);	
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПриСозданииНаСервере_ПроизвольныеФормы

Процедура ПриСозданииНаСервереПроизвольныеФормы(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	МодификацияПроизвольныхФормОбщая(Форма, Отказ, СтандартнаяОбработка);
	
	Если Форма.ИмяФормы = "Справочник.Номенклатура.Форма.РазблокированиеРеквизитов" Тогда 
		ПриСозданииНаСервере_Номенклатура_РазблокированиеРеквизитов(Форма, Отказ, СтандартнаяОбработка);
		
	ИначеЕсли Форма.ИмяФормы = "Обработка.КлиентБанк.Форма.ВыгрузкаВБанк" Тогда
		ПриСозданииНаСервере_КлиентБанк_ВыгрузкаВБанк(Форма, Отказ, СтандартнаяОбработка);
		
	ИначеЕсли Форма.ИмяФормы = "Обработка.КлиентБанк.Форма.ЗагрузкаВыписки" Тогда
		ПриСозданииНаСервере_КлиентБанк_ЗагрузкаВыписки(Форма, Отказ, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти